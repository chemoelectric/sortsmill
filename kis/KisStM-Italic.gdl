/*
Copyright (c) 2010 Barry Schwartz

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*/

//-------------------------------------------------------------------------

#include "stddef.gdh"
#include "KisStM_features.gdh"

Bidi = false;

//-------------------------------------------------------------------------

table(glyph)

    gly_f = postscript("f")
    gly_i = postscript("i")
    gly_turkish_i = postscript("i.TRK")

    cls_fflig_right = postscript("i", "l", "b", "h", "hcircumflex", "k")
    cls_fflig = postscript("f_f_i", "f_f_l", "f_f_b", "f_f_h", "f_f_hcircumflex", "f_f_k")

    cls_flig_right = (gly_f, cls_fflig_right)
    cls_flig = postscript("f_f", "f_i", "f_l", "f_b", "f_h", "f_hcircumflex", "f_k")

    cls_short_descenders = postscript(
        "g", "j", "p", "q", "y", "thorn",
        "gcircumflex", "jcircumflex", "ycircumflex",
        "yacute", "ydieresis",
        "gdotaccent", "gcommaaccent",
        "Eng", "eng",
        "uni1E57",
        "germandbls"
        )
    cls_long_descenders = postscript(
        "g.ld", "j.ld", "p.ld", "q.ld", "y.ld", "thorn.ld",
        "gcircumflex.ld", "jcircumflex.ld", "ycircumflex.ld",
        "yacute.ld", "ydieresis.ld",
        "gdotaccent.ld", "gcommaaccent.ld",
        "Eng.ld", "eng.ld",
        "uni1E57.ld",
        "germandbls.ld"
        )

    cls_st_cedilla = postscript("scedilla", "uni0163")
    cls_st_commaaccent = postscript("uni0219", "uni021B")

    cls_g_short = postscript("g", "gcircumflex", "gbreve", "gdotaccent", "gcommaaccent")
    cls_g_long = postscript("g.ld", "gcircumflex.ld", "gbreve.ld", "gdotaccent.ld", "gcommaaccent.ld")

    cls_ogoneks = postscript("aogonek", "iogonek", "iogonek.dotless", "uogonek")

    cls_f_short = postscript("f", "uni1E1F")
    cls_f_short_new = postscript("f.001", "uni1E1F.001")
    cls_f_long = postscript("f.ld", "uni1E1F.ld")
    cls_f_long_new = postscript("f.001", "uni1E1F.001")

    cls_j_short = postscript("j", "jcircumflex", "uni0237")
    cls_j_short_new = postscript("j.001", "jcircumflex.001", "uni0237.001")
    cls_j_long = postscript("j.ld", "jcircumflex.ld", "uni0237.ld")
    cls_j_long_new = postscript("j.002", "jcircumflex.002", "uni0237.002")

    cls_p_short = postscript("p", "thorn", "uni1E57")
    cls_p_short_new = postscript("p.001", "thorn.001", "uni1E57.001")
    cls_p_long = postscript("p.ld", "thorn.ld", "uni1E57.ld")
    cls_p_long_new = postscript("p.002", "thorn.002", "uni1E57.002")

    cls_y_long = postscript("y.ld", "yacute.ld", "ydieresis.ld", "ycircumflex.ld")
    cls_y_long_new = postscript("y.001", "yacute.001", "ydieresis.001", "ycircumflex.001")

    cls_pre_calt_short = (cls_g_short, cls_ogoneks);
    cls_pre_calt_long = (cls_g_long, cls_ogoneks);
    cls_calt_short = (cls_f_short, cls_j_short, cls_p_short)
    cls_calt_long = (cls_f_long, cls_j_long, cls_p_long, cls_y_long)
    cls_calt_short_new = (cls_f_short_new, cls_j_short_new, cls_p_short_new)
    cls_calt_long_new = (cls_f_long_new, cls_j_long_new, cls_p_long_new, cls_y_long_new)

endtable

//-------------------------------------------------------------------------

table(substitution)

    pass(1)
        if (ligatures == turkish)
            gly_i > gly_turkish_i;
        endif
        if (st_cedilla_style == comma_accent)
            cls_st_cedilla > cls_st_commaaccent;
        endif
        if (long_descenders)
            cls_short_descenders > cls_long_descenders;
        endif
    endpass

    pass(2)
        if (contextual_descenders)
            cls_calt_short > cls_calt_short_new / cls_pre_calt_short _;
            cls_calt_long > cls_calt_long_new / cls_pre_calt_long _;
        endif
    endpass

    pass(3)
        if (ligatures == standard || ligatures == turkish)
            gly_f gly_f cls_fflig_right > _ _ cls_fflig:(1, 2, 3);
            gly_f cls_flig_right > _ cls_flig:(1, 2);
        endif
    endpass

endtable

//-------------------------------------------------------------------------

#include "KisStM-Italic_generated_kerning.gdl"

//-------------------------------------------------------------------------
